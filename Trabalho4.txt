#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
ScannerCaseSensitive = true
ScannerTable = Compact
Input = String
Parser = LL
#RegularDefinitions
mai : [A-Z]

min : [a-z]

dig : [0-9]

letra : {mai} | {min}

// Identificados
sequenciaNMai : ({min} | {dig} | _)

sequenciaIMai : {mai} ({sequenciaNMai}{mai}?)*

sequenciaIMin : {min} ({mai}?({sequenciaNMai})+)*{mai}?

// Bloco de comentario
inicio : #\|

fim : \|#

exc : [^# \|]
#Tokens
// a: Identificador
identificador : ({sequenciaIMai} | {sequenciaIMin})

// b: Constante Inteira
constanteInteira : 0 | ([1-9] {dig}*)

// c: Constante Real
constanteReal : (0 | ([1-9] {dig}*)) \. (0 | ({dig}* [1-9]))
  
// d: Constante String
constanteString : \" [^\\ \n \"]* \"

// e: Constante Caractere
constanteCaractere : \\(s|n|t)

// f: Coment?rio de linha
:! / / [^\n]* 

// g: Comentário bloco
:! {inicio} {exc} ((#+ \|)? {exc})* {exc}? {fim}

and = identificador : "and" 
begin = identificador : "begin"
bool = identificador : "bool"
char = identificador : "char"
end = identificador : "end"
false = identificador : "false"
float = identificador : "float"
forward = identificador : "forward"
ifFalseDo = identificador : "ifFalseDo"
ifTrueDo = identificador : "ifTrueDo"
int = identificador : "int"
main = identificador : "main"
module = identificador : "module"
not = identificador : "not"
or = identificador : "or"
read = identificador : "read"
string = identificador : "string"
true = identificador : "true"
void = identificador : "void"
whileFalseDo = identificador : "whileFalseDo"
whileTrueDo = identificador : "whileTrueDo"
write = identificador : "write"
// Simbolos especiais
"+"
"-"
"*"
"/"
"("
")"
"=="
"!="
"<"
"<="
">"
">="
","
"."
";"
":"
"="
"+="
"-="
"^"

// Formatação
:[\s\n\t\r]
#NonTerminals
<program>
<listaDeclaracoes>
<listaModulos>
<corpo>
<declaracao>
<tipoModulo>
<listaParametrosFormais>
<tipo>
<parametrosFormais>
<parametro>
<listaIdentificadores>
<modulo>
<declaracaoVariaveis>
<listaComandos>
<comando>
<comando1>
<atribuicao>
<entradaDados>
<saidaDados>
<retornoModulo>
<variavel>
<operadorAtribuicao>
<expressao>
<listaExpressoes>
<selecaoOpcional>
<tipoCondicao>
<listaIdentificadores1>
<parametrosFormais1>
<listaComandos1>
<listaExpressoes1>
<elemento>
<relacional>
<aritmética>
<operadorRelacional>
<termo>
<fator>
<expressao1>
<aritmetica1>
<termo1>
<fator1>
<fator2>
<relacional1>
#Grammar
<program> ::= #15 main <listaDeclaracoes> <listaModulos> #16 begin <corpo> end #17 ;
<listaDeclaracoes> ::= î | <declaracao> <listaDeclaracoes> ;
<listaModulos> ::= î | <modulo> <listaModulos> ;

<declaracao> ::= forward module <tipoModulo> identificador "(" <listaParametrosFormais> ")" ;
<tipoModulo> ::= <tipo> | void ;
<listaParametrosFormais> ::= î | <parametrosFormais> ;
<parametrosFormais> ::= <parametro> <parametrosFormais1> ;
<parametrosFormais1> ::= î | ";" <parametrosFormais> ;
<parametro> ::=  <tipo> ":" <listaIdentificadores> ;
<tipo> ::= int | float | bool | char | string ;
<listaIdentificadores> ::= identificador #32 <listaIdentificadores1> ;
<listaIdentificadores1> ::= î | "," <listaIdentificadores> ; 

<modulo> ::= module identificador begin <corpo> end ;

<corpo> ::= <declaracaoVariaveis> <listaComandos> ;
<declaracaoVariaveis> ::= î | <variavel> <declaracaoVariaveis> ;
<variavel> ::= <tipo> #30 ":" <listaIdentificadores> #31"." ; 
<listaComandos> ::= <comando>"." <listaComandos1> ;
<listaComandos1> ::= î | <listaComandos> ;
<comando> ::= <atribuicao> | <entradaDados> | <saidaDados> | <retornoModulo> | #37 "(" <expressao> ")" <comando1> ;            
<comando1> ::=  ifTrueDo #38 <listaComandos> <selecaoOpcional> end #39 |    // selecao
	       <tipoCondicao> #41 <listaComandos> end #42 ;    // repeticao

<atribuicao> ::= identificador #32 <operadorAtribuicao> #36 <expressao> #34 ;
<operadorAtribuicao> ::= "=" | "+=" | "-=" ;

<entradaDados> ::= read "(" <listaIdentificadores> #35 ")" ;

<saidaDados> ::= write "(" <listaExpressoes> ")" ;
<listaExpressoes> ::= <expressao> #14 <listaExpressoes1> ;
<listaExpressoes1> ::= î | "," <listaExpressoes> ;
<selecaoOpcional> ::= î | #40 ifFalseDo <listaComandos> ;

<tipoCondicao> ::= whileTrueDo | whileFalseDo ;

<retornoModulo> ::= "^"<expressao> ;

<expressao> ::=  <elemento> <expressao1>;
<expressao1> ::= î | and <elemento> #18 <expressao1> | or <elemento> #19 <expressao1>;
<elemento> ::= <relacional> | true #11 | false #12 | not <elemento> #13 ;
<relacional> ::= <aritmética> <relacional1> ;
<relacional1> ::= <operadorRelacional> <aritmética> | î ;
<operadorRelacional> ::= "==" | "!=" | "<" | "<=" | ">"  | ">=" ;
<aritmética> ::= <termo> <aritmetica1>;
<aritmetica1> ::= "+" <termo> #1 <aritmetica1> | "-" <termo> #2 <aritmetica1> | î;
<termo> ::= <fator> <termo1>;
<termo1> ::= "*" <fator> #3 <termo1> | "/"<fator> #4 <termo1> | î; 
<fator> ::= identificador #33 <fator1> | 
             constanteInteira #5|
             constanteReal #6 |
             constanteCaractere #20 |
             constanteString #21 |
             "(" <expressao> ")" |
             "+" <fator> #7 |
             "-" <fator> #8 ;

<fator1> ::= î | "(" <fator2> ")";
<fator2> ::= <listaExpressoes> | î ;

